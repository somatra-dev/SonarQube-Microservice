plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version '7.2.2.6593' // working with gradle 9.2.1
    id 'jacoco'
}

group = 'com.pes-expo'
version = '0.0.1-SNAPSHOT'
description = 'rootProject'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

subprojects {
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.8.12"
    }

    plugins.withId('java') {
        tasks.withType(Test).configureEach {
            useJUnitPlatform()
            finalizedBy tasks.withType(JacocoReport)
        }

        tasks.withType(JacocoReport).configureEach {
            dependsOn tasks.named('test')
            reports {
                xml.required = true
                html.required = true
            }
        }
    }

    sonar {
        properties {
            property "sonar.coverage.jacoco.xmlReportPaths",
                    layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile.path
        }
    }
}

tasks.register('jacocoRootReport', JacocoReport) {
    reports {
        xml.required = true
        html.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco/jacocoRootReport/html")
    }
}

gradle.projectsEvaluated {
    def javaProjects = subprojects.findAll { it.plugins.hasPlugin('java') }
    tasks.named('jacocoRootReport', JacocoReport).configure {
        dependsOn javaProjects.collect { it.tasks.named('test') }
        executionData.from(javaProjects.collect {
            it.fileTree(dir: it.layout.buildDirectory, includes: ["jacoco/*.exec", "jacoco/*.ec"])
        })
        sourceDirectories.from(javaProjects.collect {
            it.extensions.getByType(SourceSetContainer).main.allSource.srcDirs
        })
        classDirectories.from(javaProjects.collect {
            it.extensions.getByType(SourceSetContainer).main.output
        })
    }
}

// SonarQube configuration
sonar {
    properties {
        property "sonar.projectKey", "pes-expo-microservices"
        property "sonar.projectName", "PES Expo Microservices"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.source", "21"
        property "sonar.token", System.getenv("SONAR_TOKEN") ?: ""
        property "sonar.exclusions", "src/main/java/com/pesexpo/rootproject/**,src/test/java/com/pesexpo/rootproject/**"
        property "sonar.coverage.jacoco.xmlReportPaths",
                layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile.path
    }
}

tasks.named('sonar') {
    dependsOn(
            ":product-service:test",
            ":order-service:test",
            "test",
            "jacocoRootReport"
    )
}
